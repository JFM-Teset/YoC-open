/*
 * Copyright (C) 2019-2020 Alibaba Group Holding Limited
 */

/******************************************************************************
 * @file     gcc_csky.h
 * @brief    csky linker file for PANGU
 * @version  V1.0
 * @date     18. Mar 2019
 ******************************************************************************/
MEMORY
{
    ROM         : ORIGIN = 0x0     , LENGTH = 0x1FFF        /* ROM  8KB*/
    SPIFLASH    : ORIGIN = 0x10000000 , LENGTH = 0x800000   /* SPIFLASH 8M */
    CPU0ISRAM   : ORIGIN = 0x20000000 , LENGTH = 0x27FFF    /* CPU0 I-SRAM 160KB*/
    CPU0DSRAM   : ORIGIN = 0x28000000 , LENGTH = 0x13FFF    /* CPU0 D-SRAM 80KB*/
    SHAREDSRAM  : ORIGIN = 0xB0000000 , LENGTH = 0x1FFF     /* Shared SRAM 8KB*/
    SDRAM       : ORIGIN = 0x30000000 , LENGTH = 0x100000   /* SDRAM 8MB*/
}

/*
__min_heap_size = 0x200;
PROVIDE (__ram_end  = 0x28014000);
PROVIDE (__heap_end = __ram_end);

REGION_ALIAS("REGION_TEXT",    CPU0ISRAM);
REGION_ALIAS("REGION_RODATA",  CPU0ISRAM);
REGION_ALIAS("REGION_DATA",    CPU0DSRAM);
REGION_ALIAS("REGION_BSS",     CPU0DSRAM);
REGION_ALIAS("REGION_SSRAM",   SHAREDSRAM);
*/
__min_heap_size = 0x200;
PROVIDE (__ram_end  = 0x30100000);
PROVIDE (__heap_end = __ram_end);
REGION_ALIAS("REGION_TEXT",    SDRAM);
REGION_ALIAS("REGION_RODATA",  SDRAM);
REGION_ALIAS("REGION_DATA",    SDRAM);
REGION_ALIAS("REGION_BSS",     SDRAM);
REGION_ALIAS("REGION_SSRAM",   SHAREDSRAM);
/*
__min_heap_size = 0x200;
PROVIDE (__ram_end  = 0x30800000);
PROVIDE (__heap_end = __ram_end);
REGION_ALIAS("REGION_TEXT",    SPIFLASH);
REGION_ALIAS("REGION_RODATA",  SPIFLASH);
REGION_ALIAS("REGION_DATA",    SDRAM);
REGION_ALIAS("REGION_BSS",     SDRAM);
REGION_ALIAS("REGION_SSRAM",   SDRAM);
*/

ENTRY(Reset_Handler)
SECTIONS
{
 .text :  {
  . = ALIGN(0x4) ;
  KEEP(*startup.o(*.text))
  __stext = . ;
  *(.text)
  *(.text*)
  *(.text.*)
  *(.gnu.warning)
  *(.stub)
  *(.gnu.linkonce.t*)
  *(.glue_7t)
  *(.glue_7)
  *(.jcr)
  *(.init)
  *(.fini)
  . = ALIGN (4) ;
  PROVIDE(__ctbp = .);
  *(.call_table_data)
  *(.call_table_text)
  . = ALIGN(0x10) ;
  __etext = . ;
 } > REGION_TEXT
 .eh_frame_hdr : {
  *(.eh_frame_hdr)
 } > REGION_TEXT
 .eh_frame : ONLY_IF_RO {
  KEEP (*(.eh_frame))
 } > REGION_TEXT
 .gcc_except_table : ONLY_IF_RO {
  *(.gcc_except_table .gcc_except_table.*)
 } > REGION_TEXT
 .rodata :  {
  . = ALIGN(0x4) ;
  __srodata = .;
  *(.rdata)
  *(.rdata*)
  *(.rdata1)
  *(.rdata.*)
  *(.rodata)
  *(.rodata1)
  *(.rodata*)
  *(.rodata.*)
  *(.rodata.str1.4)
  . = ALIGN(0x4) ;
  __ctor_start__ = .;
  KEEP (*(SORT(.ctors.*)))
  KEEP (*(.ctors))
  __ctor_end__ = .;
  KEEP (*(SORT(.dtors.*)))
  KEEP (*(.dtors))
  __dtor_end__ = .;
  . = ALIGN(0x4) ;
  __erodata = .;
 } > REGION_RODATA
 .data :  {
  . = ALIGN(0x4) ;
  __sdata = . ;
  __data_start__ = . ;
  data_start = . ;
  KEEP(*startup.o(.vectors))
  *(.got.plt)
  *(.got)
  *(.gnu.linkonce.r*)
  *(.data)
  *(.data*)
  *(.data1)
  *(.data.*)
  *(.gnu.linkonce.d*)
  *(.data1)
  *(.gcc_except_table)
  *(.gcc_except_table*)
  __start_init_call = .;
  *(.initcall.init)
  __stop_init_call = .;
  __start_cmd = .;
  *(.bootloaddata.cmd)
  . = ALIGN(4) ;
  __stop_cmd = .;
  *(.sdata)
  *(.sdata.*)
  *(.gnu.linkonce.s.*)
  *(__libc_atexit)
  *(__libc_subinit)
  *(__libc_subfreeres)
  *(.note.ABI-tag)
  . = ALIGN(0x4) ;
  __edata = .;
  __data_end__ = .;
 } > REGION_DATA AT > REGION_RODATA
 .eh_frame : ONLY_IF_RW {
  KEEP (*(.eh_frame))
 } > REGION_DATA AT > REGION_RODATA
 .gcc_except_table : ONLY_IF_RW {
  *(.gcc_except_table .gcc_except_table.*)
  __edata = .;
  __data_end__ = .;
 } > REGION_DATA AT > REGION_RODATA
 .bss : {
  . = ALIGN(0x4) ;
  __sbss = ALIGN(0x4) ;
  __bss_start__ = . ;
  *(.dynsbss)
  *(.sbss)
  *(.sbss.*)
  *(.scommon)
  *(.dynbss)
  *(.bss)
  *(.bss.*)
  *(COMMON)
  . = ALIGN(0x4) ;
  __ebss = . ;
  __end = . ;
  end = . ;
  __bss_end__ = .;
 } > REGION_BSS AT > REGION_BSS
 ._user_heap : {
  . = ALIGN(0x4) ;
  __heap_start = .;
  . += __min_heap_size;
  . = ALIGN(0x4) ;
 } > REGION_BSS AT > REGION_BSS

 .sys_sram (NOLOAD) : {
    *(.sys_sram)
 } > REGION_SSRAM

}
